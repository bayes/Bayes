         Subroutine Model(CurSet,          ! If there are N data sets, CurSet ranges from 1 to N
     C                   NoOfParams,       ! The number of nonamplitude priors given in the .params file
     C                   NoOfDerived,      ! The number of derived parameters specified in the .params file
     C                   TotalDataValues,  ! The number of hyper-complex data values in the current set
     C                   MaxNoOfDataValues,! The largest number of data values in all input sets
     C                   NoOfDataCols,     ! The number of data column specified in the .params file
     C                   NoOfAbscissaCols, ! The number of abscissa columns specified in the .params file
     C                   NoOfModelVectors, ! The number of output model vectors specified in the .params file
     C                   Params,           ! The parameters for which the model must be evaluated
     C                   Derived,          ! The output derived parameters
     C                   Abscissa,         ! The abscissa values at which the model must be evaluated
     C                   Signal)           ! The output model signal

!----------------------------------------------------------------------------------------------------------
!  Example_NoMarg.f  A prototype Example of how to write user defined model subroutines when the           |
!                    amplitudes are not marginalized from the posterior probability.                       |
!                    The prototype model is of an inversion recovery experiment.                           |
!                                                                                                          |
!  Number of priors in Example_NoMarg.params: 3                                                            |
!  Number of model vectors:                   0  Note no model vectors means no marginalizations.          |
!  Number of derived parameters:              1                                                            |
!  Number of data columns:                    1                                                            |
!                                                                                                          |
!  Ascii File Input: Two columns of the form (time data) having at least 5 data values                     |
!                                                                                                          |
!  Fid Image File: One arrayed variable having at least 5 arrayed elements.                                |
!                                                                                                          |
!  Abscissa File: Single column file containing the time values, only needed when you                      |
!    manually load 4dfp files or when the abscissa was not correctly generated from procpar.               |
!                                                                                                          |
!  Parameters: DecayRate, MzInit, MzInfty                                                                  |
!                                                                                                          |
!  Example_NoMarg.params defines three nonlinear parameter:                                                |
!     Param#      Name         Description                                                                 |
!       1         DecayRate    The DecayRate constant of the exponential                                   |
!       2         MzInti       The intensity of the signal at time t=0                                     |
!       3         MzInfty      The intensity of the signal at time t=Infty                                 |
!                                                                                                          |
!  Example_NoMarg.params defines a single output derived parameter:                                        |
!    derived(1) is the output decay time (1/DecayRate)                                                     |
!----------------------------------------------------------------------------------------------------------

        Implicit  None
        Integer,       Intent(In)::   CurSet
        Integer,       Intent(In)::   NoOfParams
        Integer,       Intent(In)::   NoOfDerived
        Integer,       Intent(In)::   TotalDataValues
        Integer,       Intent(In)::   MaxNoOfDataValues
        Integer,       Intent(In)::   NoOfDataCols
        Integer,       Intent(In)::   NoOfAbscissaCols
        Integer,       Intent(In)::   NoOfModelVectors
        Real (Kind=8), Intent(In)::   Params(NoOfParams)
        Real (Kind=8), Intent(Out)::  Derived(NoOfDerived)
        Real (Kind=8), Intent(In)::   Abscissa(NoOfAbscissaCols,MaxNoOfDataValues)
        Real (Kind=8), Intent(InOut)::Signal(NoOfDataCols,MaxNoOfDataValues)
        Integer        CurEntry
        Real (Kind=8)  Rate,AmpZero,AmpInfty

        Rate    = Params(1)
        AmpZero = Params(2)
        AmpInfty= Params(3)

        If(Rate.Eq.0D0)Then
          Derived(1) = 0D0
        Else
           Derived(1) = 1d0 / Rate
        EndIf

        Do CurEntry = 1, TotalDataValues
           Signal(1,CurEntry) =  AmpInfty + (AmpZero - AmpInfty)*Exp( - Rate * Abscissa(1,CurEntry) )
        EndDo

        Return
        End